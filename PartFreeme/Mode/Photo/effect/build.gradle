if (Boolean.valueOf(rootProject.ext.isNorthModule)) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

android {
    compileSdk rootProject.ext.android["compileSdk"]

    defaultConfig {
        if (Boolean.valueOf(rootProject.ext.isNorthModule)) {
            applicationId "com.freeme.camera.mode.photo.effect"
        }
        minSdk rootProject.ext.android["minSdk"]
        targetSdk rootProject.ext.android["targetSdk"]
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner rootProject.ext.dependencies["androidJUnitRunner"]
        if (!Boolean.valueOf(rootProject.ext.isNorthModule)) {
            consumerProguardFiles "consumer-rules.pro"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (Boolean.valueOf(rootProject.ext.isNorthModule)) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //library模式下，排除java/debug文件夹下的所有文件
                    exclude '*module'
                }
            }
        }
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    api project(path: ':PartFreeme:common')
    api project(path: ':Part3rd:Bytedance:effectAAR')
}